/*
 * resourcebundle.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * 
 * This file is part of the toolarium common-gradle-build.
 * 
 * The common-gradle-build is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * The common-gradle-build is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with Foobar. If not, see <http://www.gnu.org/licenses/>.
 */


/***************************************************************************************
 * Only if hasResourceBundle is set to true we process the resource bundle.
 ***************************************************************************************/
setCommonGradleProperty("hasResourceBundle", new File("${commonGradleConfigResourcebundlePath}").exists())
if (project.hasProperty("initResourceBundle") && project.getProperty("initResourceBundle")) {
	setCommonGradleProperty("hasResourceBundle", true)	
}


/***************************************************************************************
 * Show buildscript configuration classpath
 ***************************************************************************************/
task createResourceBundle {
	group = 'Verification'
	description = 'Create resource bundles.'
	
    doLast {	
		if (project.hasProperty("hasResourceBundle") && project.getProperty("hasResourceBundle")) {
			generateResourceBundle()
		}
    }
}
createResourceBundle.dependsOn(modelGeneratorDependency)


/***************************************************************************************
 * Generate resource bundle.
 ***************************************************************************************/
ext.generateResourceBundle = { inputFile = "${commonGradleConfigResourcebundlePath}", outputPath = "${commonGradleResourcebundleOutputPath}", jsonSupport = commonGradleResourcebundleJsonSupport ->
	if (!new File("${inputFile}").exists() && project.hasProperty("initResourceBundle") && project.getProperty("initResourceBundle")) {
		printInfo("> Initialize empty resource bundle " + colorize(HIGHLITE_LEVEL, inputFile) + ".")
		copyFile("$commonGradleConfigPath/$commonGradleConfigResourcebundleName", "${inputFile}", false)
	}
	
	if (!new File("${inputFile}").exists()) {
		return
	}
	
	printInfo("> Create resource bundle " + colorize(HIGHLITE_LEVEL, inputFile) + " into " + colorize(HIGHLITE_LEVEL, outputPath) + ".")
	ant.java(taskname: "ResourceBundle",
			classname: "jptools.util.application.GenericApplicationStarter",
			fork: "true", newenvironment: "true", failonerror: "true",
			maxmemory: "${commonGradleConfigResourcebundleMaxMemory}",
			classpath: configurations.modelGenerator.asPath) {
				jvmarg(value: "-Dfile.encoding=${fileEncoding}")
				jvmarg(value: "-Djxl.encoding=${excelFileEncoding}")
				arg(value: "-jptoolsConfig")
				arg(value: "${commonGradleConfigPath}/jptools/modelgenerator-logger.properties")
				arg(value: "-type")
				arg(value: "jptools.generator.ResourcebundleGenerator")
				arg(value: "-logInfo")
				arg(value: "ResourceBundle")
				arg(value: "-o")
				arg(value: "${outputPath}")
				
	if (!jsonSupport) {
				arg(value: "-prefix")
				arg(value: "${commonGradleResourcebundleOutputNamePrefix}")
				arg(value: "-default")
				arg(value: "true")
	} else {
				arg(value: "-json")
				arg(value: "true")
				arg(value: "-validate")
				arg(value: "true")
	}
	
	if (project.hasProperty("referenceSheetName") && project.getProperty("referenceSheetName")!=null && !project.getProperty("referenceSheetName").toString().isEmpty()) {
				arg(value: "-referenceSheetName")
				arg(value: "${referenceSheetName}")	
	}

	if (project.hasProperty("referenceSheetHasNoSubFolder") && project.getProperty("referenceSheetHasNoSubFolder")!=null && !project.getProperty("referenceSheetHasNoSubFolder").toString().isEmpty()) {
				arg(value: "-referenceSheetHasNoSubFolder")
				arg(value: "${referenceSheetHasNoSubFolder}")	
	}
	
				arg(value: "-i")
				arg(value: "${inputFile}")
				arg(value: "-modelVersion")
				arg(value: "${version}")	
			}
}
