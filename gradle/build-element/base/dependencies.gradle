/*
 * dependencies.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * 
 * This file is part of the toolarium common-gradle-build.
 * 
 * The common-gradle-build is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * The common-gradle-build is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with Foobar. If not, see <http://www.gnu.org/licenses/>.
 */

apply from: "${commonGradleBuildElementPath}/base/json.gradle"


/** 
 * Verify dependencies against white- and balcklist
 */
def commonGradleConfigWhitelistDependenciesPath = project.getProperty('commonGradleBuildUrl') + "/conf/" + project.getProperty('commonGradleConfigWhitelistDependenciesName')
if (new File("${commonGradleBuildHome}/conf/${commonGradleConfigWhitelistDependenciesName}").exists()) {
	commonGradleConfigWhitelistDependenciesPath = "${commonGradleBuildHome}/conf/${commonGradleConfigWhitelistDependenciesName}"
}
def commonGradleConfigBlacklistDependenciesPath = project.getProperty('commonGradleBuildUrl') + "/conf/" + project.getProperty('commonGradleConfigBlacklistDependenciesName')
if (new File("${commonGradleBuildHome}/conf/${commonGradleConfigBlacklistDependenciesName}").exists()) {
	commonGradleConfigBlacklistDependenciesPath = "${commonGradleBuildHome}/conf/${commonGradleConfigBlacklistDependenciesName}"
}

logInfo("Load white- and blacklist dependencies (" + colorize(HIGHLITE_LEVEL, commonGradleConfigWhitelistDependenciesPath) + colorize(INFO_LEVEL, ", ") + colorize(HIGHLITE_LEVEL, commonGradleConfigBlacklistDependenciesPath) + colorize(INFO_LEVEL, ")")); 
Properties whitelistDependencies = readPropertiesFile(commonGradleConfigWhitelistDependenciesPath)
Properties blacklistDependencies = readPropertiesFile(commonGradleConfigBlacklistDependenciesPath)


/***************************************************************************************
 * Get a dependency version
 ***************************************************************************************/
ext.getDependencyVersion = { configurationName, groupName, name ->
    def libDef = project.configurations.getByName(configurationName).allDependencies.matching {
       it.group.equals(groupName) && it.name.equals(name)
    }

	return libDef[0].version;
}


/***************************************************************************************
 * Validate a dependency version
 ***************************************************************************************/
ext.validateDependencyVersion = { configurationName, dependencyName ->
    int idx = dependencyName.indexOf(":")
    if (idx>0) {
        int lastIdx = dependencyName.indexOf(":", idx+1)
        if (lastIdx>(idx+2) && dependencyName.indexOf(":", lastIdx+1)<0) {
            return validateDependencyVersionByGroupAndName(configurationName, dependencyName.substring(0, idx), dependencyName.substring(idx+1, lastIdx), dependencyName.substring(lastIdx+1))
        } else {
            addError("Could not validate dependency ${dependencyName}!")
        }
    } else {
        addError("Could not validate dependency ${dependencyName}!")
    }
}


/***************************************************************************************
 * Validate a dependency version by group and name
 ***************************************************************************************/
ext.validateDependencyVersionByGroupAndName = { configurationName, groupName, name, expectedVersion ->
    boolean isValid = false
    def versionToCheck = getDependencyVersion(configurationName, groupName, name)
	if (!isCompliantVersion("${expectedVersion}", "${versionToCheck}")) {
        //printInfo("> Dependency " + colorize(HIGHLITE_LEVEL, groupName + ":" + name + ":" + versionToCheck) + NELINE + "    must be updated to at least version " + colorize(HIGHLITE_LEVEL, expectedVersion) + " to be compatible!")
        printWarn("Dependency " + colorize(HIGHLITE_LEVEL, groupName + ":" + name + ":" + versionToCheck) + NELINE + colorize(WARN_LEVEL, "    must be updated to at least version ") + colorize(HIGHLITE_LEVEL, expectedVersion) + colorize(WARN_LEVEL, " to be compatible!"))
    } else {
        isValid = true
    }

	if (project.hasProperty('isReleaseUpdateVersion') && "true".equalsIgnoreCase("" + project.getProperty('isReleaseUpdateVersion'))) {
		ext.validBuild=false
		ext.validBuildMessage="Incompatible dependency: " + groupName + ":" + name + " -> " + versionToCheck + " < " + expectedVersion + "!"
        //addError("Incompatible dependency: " + groupName + ":" + name + " -> " + versionToCheck + " < " + expectedVersion + "!")
    }

	return isValid;
}


/***************************************************************************************
 * Get the version expression
 ***************************************************************************************/
ext.getVersionExpression = { Properties properties, sourceName, dependencyGroupName, dependencyName ->
	def versionExpression = properties.getProperty(("" + dependencyGroupName).toString())
	if (versionExpression!=null && !versionExpression.toString().trim().isEmpty()) {
		logInfo("Version expression found, groupname " + dependencyGroupName + " => " +versionExpression)
	    return versionExpression;
	}		

	versionExpression = properties.getProperty(("" + dependencyGroupName + "." + dependencyName).toString())
	if (versionExpression!=null && !versionExpression.toString().trim().isEmpty()) {
		logInfo("Version expression found, name " + dependencyGroupName + "." + dependencyName + " => " +versionExpression)
	    return versionExpression;
	}
	
	versionExpression = properties.getProperty(("[" + sourceName + "]" + dependencyGroupName).toString())
	if (versionExpression!=null && !versionExpression.toString().trim().isEmpty()) {
		logInfo("Version expression in section [" + sourceName + "] found, groupname " + dependencyGroupName + " => " +versionExpression)
	    return versionExpression;
	}		
	
	versionExpression = properties.getProperty(("[" + sourceName + "]" + dependencyGroupName + "." + dependencyName).toString())
	if (versionExpression!=null && !versionExpression.toString().trim().isEmpty()) {
		logInfo("Version expression in section [" + sourceName + "] found, name " + dependencyGroupName + "." + dependencyName + " => " +versionExpression)
	    return versionExpression;
	}		
	return null
}


/***************************************************************************************
 * Validate a dependency
 ***************************************************************************************/
def dependencyReportedMap = [:]
ext.validateDependency = { sourceName, dependencyGroupName, dependencyName, dependencyVersion ->
	if (dependencyVersion == null) {
		return false;
	}
	
	def depVersion=("" + dependencyVersion).toString()
	def whitelistVersionExpression = getVersionExpression(whitelistDependencies, sourceName, dependencyGroupName, dependencyName)
	if (whitelistVersionExpression!=null && !whitelistVersionExpression.toString().trim().isEmpty()) {
		def inSourceTree = false
		if (whitelistVersionExpression.startsWith("*")) {
			inSourceTree = true
			whitelistVersionExpression = whitelistVersionExpression.substring(1).trim()
		}
		if (isCompliantVersion("${whitelistVersionExpression}", "${depVersion}")) {
			logInfo("[" + colorize(HIGHLITE_LEVEL, sourceName) + colorize(INFO_LEVEL, "] "
					+ "Valid dependency ") + colorize(HIGHLITE_LEVEL, dependencyName) + colorize(INFO_LEVEL, " v") + colorize(HIGHLITE_LEVEL, depVersion) 
					+ colorize(INFO_LEVEL, " (") + colorize(HIGHLITE_LEVEL, whitelistVersionExpression) + colorize(INFO_LEVEL, ").") ); 
			return true
		}
	}
	
	def blacklistVersionExpression = getVersionExpression(blacklistDependencies, sourceName, dependencyGroupName, dependencyName)
	if (blacklistVersionExpression!=null && !blacklistVersionExpression.toString().trim().isEmpty()) {
		def inSourceTree = false
		if (blacklistVersionExpression.startsWith('*')) {
			inSourceTree = true
			blacklistVersionExpression = blacklistVersionExpression.substring(1).trim()
		}

		if (isCompliantVersion("${blacklistVersionExpression}", "${depVersion}")) {
			logInfo("[" + colorize(HIGHLITE_LEVEL, sourceName) + colorize(INFO_LEVEL, "] "
					+ "Invalid dependency ") + colorize(HIGHLITE_LEVEL, dependencyName) + colorize(INFO_LEVEL, " v") + colorize(HIGHLITE_LEVEL, depVersion) 
					+ colorize(INFO_LEVEL, " (") + colorize(HIGHLITE_LEVEL, blacklistVersionExpression) + colorize(INFO_LEVEL, ").") ); 
			
			def key = (dependencyGroupName + ":" + dependencyName + ":" + depVersion).toString()
			if (!dependencyReportedMap.containsKey(key)) {
				addError("[" + sourceName + "] Invalid dependency found ${dependencyName} v${depVersion} -> blacklisted (${blacklistVersionExpression})")
				dependencyReportedMap.put(key, blacklistVersionExpression)
			}
			
			return false;
		}
	}
	
	return true
}


/** 
 * Prepare dependencies json file
 */
ext.prepareDependenciesJson = { inputFilename, outputFilename -> 
	File inputFile = new File(inputFilename)
	File outputFile = new File(outputFilename)
	
	if (!inputFile.exists()) {
		printWarn("Could not find file " + inputFile.getName() + "!")
		return
	}
	
	if (outputFile.exists()) {
		outputFile.delete()
	}

	try {			
		logInfo("Create " + colorize(HIGHLITE_LEVEL, "${outputFilename}") + colorize(INFO_LEVEL, " from source ") + colorize(HIGHLITE_LEVEL, "${outputFilename}") + colorize(INFO_LEVEL, "."))
	    def newLine = System.getProperty("line.separator")
	    def replacedVar = false
		inputFile.withReader {
			def line
			while ((line = it.readLine()) != null) {
				//var projectDependencyReport = 
				if (!replacedVar && line.indexOf("var") >= 0) {
					def idx = line.indexOf("=")
					if (idx>=0) {
					   line = line.substring(idx + 1)
					   replacedVar = true
					}
				}
				
				outputFile.append(line.replace(';', ' ') + newLine)
			}
		}
	} catch (Exception e) {
		printWarn("Could not process file " + inputFile.getName() + ": " + e.getMessage())
		throw e
	}			
}


/** 
 * Verify the direct dependencies
 */
task verifyDependencies() {
	doFirst {
		def configurationList = []
		def implementationList = []
		def dependencyMap = [:]
		project.configurations.all {
			configurationList.add(it.name)

			try {
				it.incoming.dependencies.all { dep ->
					def list = []
					list = dependencyMap.get(it.name.toString())
					if (list == null) {
					   list = []
					}
					// TODO:printInfo "=>"+it.getClass().getName()
					def dependencyVersion = ""
					if (dep.version != null) {
						dependencyVersion = ("" + dep.version).toString()
					}
					
					validateDependency(it.name, dep.group, dep.name, dependencyVersion);
					list.add(("" + dep.group + ":" + dep.name + ":" + dependencyVersion).toString())
					dependencyMap.put(it.name.toString(), list)
				}
			} catch (Exception e) {
				e.printStackTrace()
			}
		}
		logInfo "Configuratiin: $configurationList"
		printInfo "> Implementation: " + dependencyMap.get("implementation")
		printInfo "> RuntimeClasspath: " + dependencyMap.get("runtimeClasspath")
		printInfo "> CompileOnly: " + dependencyMap.get("compileOnly")
		//printInfo "> Keys: " + dependencyMap.keySet()
	}
}


/***************************************************************************************
 * project validation
 ***************************************************************************************/
if (!project.getProperty('IS_NEW') && project.hasProperty('dependencyTextReport') && project.getProperty('dependencyTextReport')) {
    projectValidation.finalizedBy("dependencyReport")
}
if (!project.getProperty('IS_NEW') && project.hasProperty('dependencyHTMLReport') && project.getProperty('dependencyHTMLReport')) {
    projectValidation.finalizedBy("htmlDependencyReport")
}

dependencyReport {
	try {
		outputFile = file("$dependenciesReport")
	} catch (Exception e) {
		logDebug("Could not set outputfile [$dependenciesReport]: " + e.getMessage())
	}
}

htmlDependencyReport {
	reports.html.outputLocation = file("$dependenciesReportPath")
	
	// gradle 8.13 set new outputFile attribute
	try {
		outputFile = file("$dependenciesReport")
	} catch (Exception e) {
		logDebug("Could not set outputfile [$dependenciesReport]: " + e.getMessage())
	}
	
    doLast {
	    // prepare dependency json
		prepareDependenciesJson("${dependenciesReportPath}/root.js", "${dependenciesFile}")
	}
}
