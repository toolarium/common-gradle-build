/*
 * version.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * MIT License: https://mit-license.org
 */


/***************************************************************************************
 * Read version: see https://semver.org/ 
 ***************************************************************************************/
ext.readVersion = { ->

	// read version number
	logInfo("Read version file " + colorize(HIGHLITE_LEVEL, versionFile))

	def props = new Properties()	
	file(versionFile).withInputStream { props.load(it) }	
	
	def majorNumber = props.getProperty("major.number")
	if (majorNumber!=null) {
		setCommonGradleProperty('majorVersionNumber', parseInteger(majorNumber, 0, 'major.number'))
	}

	def minorNumber = props.getProperty("minor.number")
	if (minorNumber!=null) {
		setCommonGradleProperty('minorVersionNumber', parseInteger(minorNumber, 0, 'minor.number'))
	}
	
	def doUpdate = false
	def microNumber = props.getProperty("micro.number")
	if (microNumber!=null) {
		setCommonGradleProperty('microVersionNumber', parseInteger(microNumber, 1, 'micro.number'))
	} else {
		// keep backwards compatibility
		def revisionNumber = props.getProperty("revision.number")
		if (revisionNumber!=null) {
			setCommonGradleProperty('microVersionNumber', parseInteger(revisionNumber, 1, 'revision.number'))
			doUpdate = true
		}
	}
	
	def qualifierNumber = props.getProperty("qualifier")
	if (qualifierNumber!=null) {
		setCommonGradleProperty('qualifierVersionNumber', qualifierNumber.trim(), '')
	} else {
		// keep backwards compatibility
		def buildNumber = props.getProperty("build.number")
		if (buildNumber!=null) {
			setCommonGradleProperty('qualifierVersionNumber', buildNumber.trim(), '')
			doUpdate = true
		}
	}	
	
	project.version = project.getProperty('majorVersionNumber') + "." + project.getProperty('minorVersionNumber') + "." + project.getProperty('microVersionNumber')
	def qualifierVersionNumber = project.getProperty('qualifierVersionNumber')
	if (qualifierVersionNumber !=null && !qualifierVersionNumber.isEmpty()) {
		project.version = project.version + "-" +  qualifierVersionNumber
	}

	if (!project.getProperty('qualifierVersionNumber').endsWith(SNAPSHOT_TAG_NAME)) {
		setCommonGradleProperty('isReleaseVersion', true, true, false)
		logInfo("It is a release verison: " + colorize(HIGHLITE_LEVEL, project.version))
	} else {
		setCommonGradleProperty('isReleaseVersion', false, false, false)
		logInfo("It is a snapshot verison: " + colorize(HIGHLITE_LEVEL, project.version))
	}

	setCommonGradleProperty('isSnapshotVersion', !project.getProperty('isReleaseVersion'))
	setCommonGradleProperty('scmVersionNumber', '')
	
	if (doUpdate) {
		updateVersion()
	}
	
}


/***************************************************************************************
 * Update version
 ***************************************************************************************/
ext.updateVersion = { ->
	logInfo("Write file " + versionFile + " -> " + project.getProperty('majorVersionNumber') + ", " + project.getProperty('minorVersionNumber') + ", " + project.getProperty('microVersionNumber') + ", " + project.getProperty('qualifierVersionNumber'))
	new File('.', versionFile).withWriter { w ->
		w << "major.number        = " << project.getProperty('majorVersionNumber') << NELINE \
		<< "minor.number        = " << project.getProperty('minorVersionNumber') << NELINE \
		<< "micro.number        = " << project.getProperty('microVersionNumber') << NELINE \
		<< "qualifier           = " << project.getProperty('qualifierVersionNumber') << NELINE 
	}
}


/***************************************************************************************
 * Increase major version
 ***************************************************************************************/
ext.increaseMajorVersion = { ->
	setCommonGradleProperty('majorVersionNumber', 1 + project.getProperty('majorVersionNumber'), 1)
	setCommonGradleProperty('minorVersionNumber', 0, 0)
	setCommonGradleProperty('microVersionNumber', 0, 0)
	logInfo("Increase major number " + majorVersionNumber + ": " + project.version)
	updateVersion()
	readVersion()
}


/***************************************************************************************
 * Increase minor version
 ***************************************************************************************/
ext.increaseMinorVersion = { ->
	setCommonGradleProperty('minorVersionNumber', 1 + project.getProperty('minorVersionNumber'), 1)
	setCommonGradleProperty('microVersionNumber', 0, 0)
	logInfo("Increase minor number " + minorVersionNumber + ": " + project.version)
	updateVersion()
	readVersion()
}


/***************************************************************************************
 * Increase micro version
 ***************************************************************************************/
ext.increaseMicroVersion = { ->
	setCommonGradleProperty('microVersionNumber', 1 + project.getProperty('microVersionNumber'), 1)
	logInfo("Increase micro number " + microVersionNumber + ": " + project.version)
	updateVersion()
	readVersion()
}


/***************************************************************************************
 * Read the remote version
 ***************************************************************************************/
ext.readRemoteVersion = { url ->

	if (!HAS_ONLINE_CONNECTION) {
		return ""
	}
	
	Properties props = new Properties();
	props.load(new URL(url).openStream())

	def majorNumber = props.getProperty("major.number")
	def minorNumber = props.getProperty("minor.number")
	def microNumber = props.getProperty("micro.number")
	def qualifier = props.getProperty("qualifier")
	
	def remoteVersion = majorNumber + "." + minorNumber;	
	if (microNumber!=null) {
		remoteVersion += "." + microNumber
	} else if (qualifier!=null) { 
		remoteVersion += "-" + qualifier
	}
	
	return remoteVersion
}
