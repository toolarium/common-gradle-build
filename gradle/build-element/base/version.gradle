/*
 * version.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * 
 * This file is part of the toolarium common-gradle-build.
 * 
 * The common-gradle-build is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * The common-gradle-build is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with Foobar. If not, see <http://www.gnu.org/licenses/>.
 */


buildscript {
	repositories {
        if (Integer.valueOf(gradle.gradleVersion.toString().split('\\.')[0])<7) {
			jcenter()
		} else {
			mavenCentral()
			maven {
				url "https://mvnrepository.com/artifact"
				
			}
		}
	}
	dependencies {
		def semver4jVersion="3.1.0"
		if (project.hasProperty('semver4jVersion')) {
			semver4jVersion = project.getProperty('semver4jVersion')
		}
		logInfo("smver4j version: " + colorize(HIGHLITE_LEVEL, semver4jVersion) + colorize(INFO_LEVEL, ".")); 
		classpath "com.vdurmont:semver4j:${semver4jVersion}"
	}
}


/***************************************************************************************
 * Read version: see https://semver.org/ 
 ***************************************************************************************/
ext.readVersion = { ->
	// read version number
	logInfo("Read version file " + colorize(HIGHLITE_LEVEL, versionFile))

	def props = new Properties()	
	if (new File(versionFile).canRead()) {
		file(versionFile).withInputStream { props.load(it) }	
	} else {
		printWarn("Could not read version file " + colorize(HIGHLITE_LEVEL, versionFile))
	}
	
	def majorNumber = props.getProperty("major.number")
	if (majorNumber!=null) {
		setCommonGradleProperty('majorVersionNumber', parseInteger(majorNumber, 0, 'major.number'))
	}

	def minorNumber = props.getProperty("minor.number")
	if (minorNumber!=null) {
		setCommonGradleProperty('minorVersionNumber', parseInteger(minorNumber, 0, 'minor.number'))
	}
	
	def doUpdate = false
	def revisionNumber = props.getProperty("revision.number")
	if (revisionNumber!=null) {
		setCommonGradleProperty('revisionVersionNumber', parseInteger(revisionNumber, 1, 'revision.number'))
	} else {
		// keep backwards compatibility
		revisionNumber = props.getProperty("micro.number")
		if (revisionNumber!=null) {
			setCommonGradleProperty('revisionVersionNumber', parseInteger(revisionNumber, 1, 'revision.number'))
			doUpdate = true
		}
	}
	
	def qualifierNumber = props.getProperty("qualifier")
	if (qualifierNumber!=null) {
		setCommonGradleProperty('qualifierVersionNumber', qualifierNumber.trim(), '')
	} else {
		// keep backwards compatibility
		def buildNumber = props.getProperty("build.number")
		if (buildNumber!=null) {
			setCommonGradleProperty('qualifierVersionNumber', buildNumber.trim(), '')
			doUpdate = true
		}
	}	

	if (project.hasProperty('isReleaseUpdateVersion') && "true".equalsIgnoreCase("" + project.getProperty('isReleaseUpdateVersion')))  {
		setCommonGradleProperty("isReleaseVersion", true)
		setCommonGradleProperty("tagIgnoreReleaseUpdateVersionIfNoFilesChanged", false) // in case there is a release version, we don't ignore anything!		
		setCommonGradleProperty('qualifierVersionNumber', "${project.ext.BUILD_TIMESTAMP_ID}".toString().substring(0, 14))
    }

	if (project.hasProperty('isReleaseVersion')) {
		if ("true".equalsIgnoreCase("" + project.getProperty('isReleaseVersion')) && project.getProperty('qualifierVersionNumber').endsWith(SNAPSHOT_TAG_NAME)) {
			qualifierVersionNumber=qualifierVersionNumber.replace(SNAPSHOT_TAG_NAME, "")
			doUpdate = true
		}
		
		if ("false".equalsIgnoreCase("" + project.getProperty('isReleaseVersion')) && !project.getProperty('qualifierVersionNumber').endsWith(SNAPSHOT_TAG_NAME)) {
			if (project.getProperty('qualifierVersionNumber').toString().trim().isEmpty()) {
				qualifierVersionNumber="${SNAPSHOT_TAG_NAME}"
			} else {
				qualifierVersionNumber="${qualifierVersionNumber}-${SNAPSHOT_TAG_NAME}"
			}
			doUpdate = true
		}
	}

	project.version = project.getProperty('majorVersionNumber') + "." + project.getProperty('minorVersionNumber') + "." + project.getProperty('revisionVersionNumber')
	def qualifierVersionNumber = project.getProperty('qualifierVersionNumber')
	if (qualifierVersionNumber != null && !qualifierVersionNumber.isEmpty()) {
		project.version = project.version + "-" +  qualifierVersionNumber
	}

	if (!project.getProperty('qualifierVersionNumber').endsWith(SNAPSHOT_TAG_NAME)) {
		setCommonGradleProperty('isReleaseVersion', true, true, false)
		logInfo("It is a release verison: " + colorize(HIGHLITE_LEVEL, project.version))
	} else {
		setCommonGradleProperty('isReleaseVersion', false, false, false)
		logInfo("It is a snapshot verison: " + colorize(HIGHLITE_LEVEL, project.version))
	}

	setCommonGradleProperty('isSnapshotVersion', !project.getProperty('isReleaseVersion'))
	
	if (doUpdate) {
		updateVersion()
	}	
	
	return doUpdate
}


/***************************************************************************************
 * Update version
 ***************************************************************************************/
ext.updateVersion = { ->
	logInfo("Write file " + versionFile + " -> " + project.getProperty('majorVersionNumber') + ", " + project.getProperty('minorVersionNumber') + ", " + project.getProperty('revisionVersionNumber') + ", " + project.getProperty('qualifierVersionNumber'))
	new File(versionFile).withWriter { w ->
		w << "major.number        = " << project.getProperty('majorVersionNumber') << NELINE \
		<< "minor.number        = " << project.getProperty('minorVersionNumber') << NELINE \
		<< "revision.number     = " << project.getProperty('revisionVersionNumber') << NELINE \
		<< "qualifier           = " << project.getProperty('qualifierVersionNumber') << NELINE 
	}
}


/***************************************************************************************
 * Increase major version
 ***************************************************************************************/
ext.increaseMajorVersion = { ->
	setCommonGradleProperty('majorVersionNumber', 1 + project.getProperty('majorVersionNumber'), 1)
	setCommonGradleProperty('minorVersionNumber', 0, 0)
	setCommonGradleProperty('revisionVersionNumber', 0, 0)
	logInfo("Increase major number " + majorVersionNumber + ": " + project.version)
	updateVersion()
	readVersion()
}


/***************************************************************************************
 * Increase minor version
 ***************************************************************************************/
ext.increaseMinorVersion = { ->
	setCommonGradleProperty('minorVersionNumber', 1 + project.getProperty('minorVersionNumber'), 1)
	setCommonGradleProperty('revisionVersionNumber', 0, 0)
	logInfo("Increase minor number " + minorVersionNumber + ": " + project.version)
	updateVersion()
	readVersion()
}


/***************************************************************************************
 * Increase revision version
 ***************************************************************************************/
ext.increaseRevisionVersion = { ->
	setCommonGradleProperty('revisionVersionNumber', 1 + project.getProperty('revisionVersionNumber'), 1)
	logInfo("Increase revision number " + revisionVersionNumber + ": " + project.version)
	updateVersion()
	readVersion()
}


/***************************************************************************************
 * Parse a version number
 ***************************************************************************************/
import com.vdurmont.semver4j.Semver;
ext.parseVersion = { inputVersion, strict = true ->
	if (inputVersion == null || inputVersion.toString().trim().isEmpty()) {
	    return null;
	}

	try {
		if (strict) {
			logInfo("Parse strict version number " + inputVersion + ".")
			return new Semver(inputVersion.toString());
		}
		
		logInfo("Parse version number " + inputVersion + ".")
		return new Semver(inputVersion.toString(), SemverType.IVY);
	} catch (Exception e) {
		logInfo("Invalid version number " + inputVersion + ": " + e.getMessage())
		return null;
	}
}


/***************************************************************************************
 * Check if the version is compliant
 ***************************************************************************************/
ext.isCompliantVersion = { referenceVersion, versionToCheck ->
	if (referenceVersion == null || referenceVersion.toString().trim().isEmpty()) {
	    return true;
	}
	
	if (versionToCheck == null || versionToCheck.toString().trim().isEmpty()) {
	    return false;
	}

	def refSemVer = parseVersion(referenceVersion, false);
	if (refSemVer == null) {
	    return true;
	}
	
	def semVer = parseVersion(versionToCheck);
	if (semVer == null) {
	    return false;
	}
	
	return semVer.satisfies(referenceVersion); // e.g. 1.2.+
}

/***************************************************************************************
 * Check if the version is compliant
 ***************************************************************************************/
ext.parseNameWithVersion = { nameWithVersion ->
	// parse semver version
	String fileName = nameWithVersion
	String fileVersion = ""
	String[] ps = fileName.split("-")
	if (parseVersion(ps[ps.length - 1])!=null) {
		fileName = fileName.substring(0, fileName.length() - (ps[ps.length - 1].length() + 1))
		fileVersion = parseVersion(ps[ps.length - 1])
	} else if (parseVersion(ps[ps.length - 2] + "-" + ps[ps.length - 1])!=null) {
		fileName = fileName.substring(0, fileName.length() - (ps[ps.length - 2].length() + ps[ps.length -1].length() + 2))
		fileVersion = parseVersion(ps[ps.length - 2] + "-" + ps[ps.length - 1])
	}
	
	return new String[] { fileName, fileVersion };
}
