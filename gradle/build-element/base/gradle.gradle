/*
 * gradle.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * 
 * This file is part of the toolarium common-gradle-build.
 * 
 * The common-gradle-build is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * The common-gradle-build is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with Foobar. If not, see <http://www.gnu.org/licenses/>.
 */

 
/***************************************************************************************
 * Read gradle wrapper version
 ***************************************************************************************/
ext.readGradleWrapperVersion = { ->
	def versionFile = 'gradle/wrapper/gradle-wrapper.properties'

	if (new File(versionFile).exists()) {
		logInfo("Read gradle wrapper version file " + colorize(HIGHLITE_LEVEL, versionFile))
		def props = new Properties()
		file(versionFile).withInputStream { props.load(it) }
		String url = props.getProperty('distributionUrl').toString()
		
		if (url!=null) {
			String[] list = url.split('/');
			
			if (list!=null && list.length>0) {
				def filename = list[list.length-1];
				if (filename != null) {
					String[] filenameSplit = filename.split('-');
					if (filenameSplit!=null && filenameSplit.length>1) {
						return filenameSplit[1];
					} else {
						return filename;
					}
				}
			}
			
			return url;
		}
	}
	
	return gradle.gradleVersion;
}
 

/***************************************************************************************
 * Install gradle wrapper if not exists
 ***************************************************************************************/
ext.gradleWrapperinstalled = false
def hasWrapperFiles=(new File("gradle").exists() \
						&& new File("gradle/wrapper").exists() \
						&& new File("gradle/wrapper/gradle-wrapper.jar").exists() \
						&& new File("gradle/wrapper/gradle-wrapper.properties").exists() \
						&& new File("gradlew").exists() \
						&& new File("gradlew.bat").exists())

logInfo("> Expected gradle wrapper version " + colorize("$gradleWrapperDefaultVersion") + colorize(INFO_LEVEL, ", found version ") + colorize("$gradle.gradleVersion") + colorize(INFO_LEVEL, "."))
def hasExpectedWrapperVersion=(!project.hasProperty("gradleWrapperDefaultVersion") || "$gradleWrapperDefaultVersion".toString().trim().isEmpty() || "$gradleWrapperDefaultVersion".toString().trim().equals("$gradle.gradleVersion".toString().trim()))

if (!hasExpectedWrapperVersion && project.hasProperty('isReleaseUpdateVersion') && "true".equalsIgnoreCase("" + project.getProperty('isReleaseUpdateVersion'))) {
	printInfo("> Ignore gradle wrapper update to version " + colorize("${gradleWrapperDefaultVersion}") + ".")
	hasExpectedWrapperVersion = true
}
setCommonGradleProperty("hasExpectedWrapperVersion", hasExpectedWrapperVersion);

wrapper {
	onlyIf { installGradleWrapper && (!hasExpectedWrapperVersion || !hasWrapperFiles) }
	gradleVersion = "$gradleWrapperDefaultVersion"
			
	doLast {		
		if (!gradleWrapperinstalled) {
			gradleWrapperinstalled = true
			printInfo("> Installed gradle wrapper version " + colorize(readGradleWrapperVersion()))
			
			if (project.hasProperty('initScmWithNewFiles') && project.getProperty('initScmWithNewFiles')) {
				Set result = new TreeSet()
				result.add("gradle/wrapper/gradle-wrapper.jar")
				result.add("gradle/wrapper/gradle-wrapper.properties")
				result.add("gradlew")
				result.add("gradlew.bat")
				addGit(result)
				//commitGit("Update gradle wrapper to version " + readGradleWrapperVersion())
			}
		}
	}
}

// dependency on project validation
projectValidation.dependsOn wrapper
