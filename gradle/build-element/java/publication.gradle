/*
 * publication.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * MIT License: https://mit-license.org
 */
 
apply plugin: 'maven-publish'


/***************************************************************************************
 * Repository publishing
 ***************************************************************************************/
publishing {
	def description = ''
	if (project.hasProperty('rootDescription')) {
		description = project.getProperty('rootDescription')
	}

	def mavenUsername = ''
	if (project.hasProperty('mavenUsername')) {
		mavenUsername = project.getProperty('mavenUsername')
	}

	def mavenPassword = ''
	if (project.hasProperty('mavenPassword')) {
		mavenPassword = project.getProperty('mavenPassword')
	}

	def deployUrl = mavenUrl
	if (project.ext.isSnapshotVersion) {
		deployUrl = mavenSnapshotUrl
	}
	
	publications {
        mavenJava(MavenPublication) {
            artifactId = "${rootProject.name}"
			groupId= "${rootGroupId}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
			
			// after staging go to -> https://oss.sonatype.org/#stagingRepositories -> close
            pom {
                name = "${rootProject.name}"
                description = description
                url = "${rootUrl}"
				
                licenses {
                    license {
                        name = "${LICENSE}"
                        url = "${LICENSE_URL}"
                    }
                }
            }
        }
	}
	
	repositories {
        maven {
            url deployUrl
            credentials {
                    username mavenUsername
                    password mavenPassword
            }
        }
    }
}
