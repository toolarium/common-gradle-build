/*
 * java.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * MIT License: https://mit-license.org
 */

apply plugin: 'java'


/***************************************************************************************
 * Compile
 ***************************************************************************************/
tasks.withType(JavaCompile) {
	options.encoding = "${ENCODING}"
	options.compilerArgs << '-Xlint:unchecked'
	options.deprecation = true
	sourceCompatibility = project.getProperty('sourceCompatibility')
    targetCompatibility = project.getProperty('targetCompatibility')

	def sourceVersion= parseVersion(sourceCompatibility)
	def targetVersion= parseVersion(targetCompatibility)
	if (!targetVersion.isCompatibleWith(sourceVersion)) {
		logInfo("The target (" + colorize(HIGHLITE_LEVEL, targetCompatibility) + colorize(INFO_LEVEL, ") is not compatible with the source (" ) + colorize(HIGHLITE_LEVEL, sourceCompatibility) + colorize(INFO_LEVEL, ") compatibility!"))
	}
	
	logInfo("Java compiler: sourceCompatibility: " + colorize(HIGHLITE_LEVEL, sourceCompatibility) \
			+ colorize(INFO_LEVEL, ", targetCompatibility: ") + colorize(HIGHLITE_LEVEL, targetCompatibility) \
			+ colorize(INFO_LEVEL, ", encoding: ") + colorize(HIGHLITE_LEVEL, options.encoding) \
			+ colorize(INFO_LEVEL, ", deprecation: ") + colorize(HIGHLITE_LEVEL, options.deprecation) \
			+ colorize(INFO_LEVEL, ", fork: ") + colorize(HIGHLITE_LEVEL, options.fork) \
			+ colorize(INFO_LEVEL, ", args: ") + colorize(HIGHLITE_LEVEL, options.allCompilerArgs))
}


/***************************************************************************************
 * Build jar file
 ***************************************************************************************/
jar {
	// include readme, license and version files
	into 'META-INF', {
		//from "${readmeFile}"  // 'README.md'
		from "${licenseFile}" // 'LICENSE'
		from "${versionFile}" // 'VERSION'
	}

	logInfo("Set root package name to [" + colorize(HIGHLITE_LEVEL, rootPackageName) + colorize(INFO_LEVEL, "]  -> ") + colorize(HIGHLITE_LEVEL, sourceSets.main.allJava.srcDirs))

	// define proper manifest
	manifest {
		attributes 'Specification-Title'   : rootProject.name,
				   'Specification-Version' : project.version,
				   'Implementation-Title'  : rootProject.name,
				   'Implementation-Version': project.version + project.ext.scmVersionNumber,
				   'Built-By'			   : System.properties['user.name'],
				   'Build-Timestamp'	   : BUILD_TIMESTAMP,
				   'Created-By'			   : "Gradle ${gradle.gradleVersion}",
				   'Build-JDK'			   : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				   'Build-OS'			   : "${System.properties['os.name']} (${System.properties['os.version']}), ${System.properties['os.arch']}"
	}
}


/***************************************************************************************
 * Build sources jar file
 ***************************************************************************************/
task sourcesJar(type: Jar, dependsOn: classes) {
	group = 'Build'
	description = 'Assembles the sources into a jar file'

	from sourceSets.main.allJava, {
		//include "${rootPackageName}/**" 
	}
	
	// include readme, license and version files
	into 'META-INF', {
		//from "${readmeFile}"  // 'README.md'
		from "${licenseFile}" // 'LICENSE'
		from "${versionFile}" // 'VERSION'
	}

	archiveClassifier = 'sources'
	//archiveAppendix = 'sources'

	logInfo("Set root package name to [" + colorize(HIGHLITE_LEVEL, rootPackageName) + colorize(INFO_LEVEL, "]  -> ") + colorize(HIGHLITE_LEVEL, sourceSets.main.allJava.srcDirs))

	// define proper manifest
	manifest {
		attributes 	'Specification-Title'   : rootProject.name,
					'Specification-Version' : project.version,
					'Implementation-Title'  : rootProject.name,
					'Implementation-Version': project.version + project.ext.scmVersionNumber,
					'Built-By'			   	: System.properties['user.name'],
					'Build-Timestamp'	   	: BUILD_TIMESTAMP,
					'Created-By'		   	: "Gradle ${gradle.gradleVersion}",
					'Build-JDK'			   	: "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
					'Build-OS'			   	: "${System.properties['os.name']} (${System.properties['os.version']}), ${System.properties['os.arch']}"
	}		
 }
