/*
 * java-application.gradle
 *
 * Copyright by toolarium, all rights reserved.
 * 
 * This file is part of the toolarium common-gradle-build.
 * 
 * The common-gradle-build is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * The common-gradle-build is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with Foobar. If not, see <http://www.gnu.org/licenses/>.
 */


/***************************************************************************************
 * Set additional template path
 ***************************************************************************************/
ext.projectTypeTemplatePath='java-application'
ext.className='MyApplication'
ext.commonGradleResourcebundleOutputPath='build/resources/main'


if (!project.hasProperty('commonGradleBuildUrl')) {
    ext.commonGradleBuildUrl='https://raw.githubusercontent.com/toolarium/common-gradle-build/master/gradle'
}

if (!project.hasProperty('commonGradleBuildElementPath')) {
	ext.commonGradleBuildElementPath="${commonGradleBuildUrl}/build-element"
}

// set docker image standards
if (!project.hasProperty("dockerImage")) {
	project.ext.set("dockerImage", "adoptopenjdk/openjdk11:alpine-jre")
}
if (!project.hasProperty("dockerDefaultJavaOptions")) {
	project.ext.set("dockerDefaultJavaOptions", "-Djava.security.egd=file:/dev/./urandom")
}

apply from: "${commonGradleBuildUrl}/java-library.gradle"
apply plugin: 'application'

// default classifier name
def shadowClassifierContext = ""
if (project.hasProperty("javaApplicationShadowClassifier") && !project.getProperty("javaApplicationShadowClassifier").toString().isEmpty()) {
	shadowClassifierContext = "-" + project.getProperty("javaApplicationShadowClassifier")
}

setCommonGradleProperty('jarRunnerFile', "${project.name}-${project.version}${shadowClassifierContext}.jar");

if (!project.hasProperty("dockerDeploymentSourcePath")) {
	//project.ext.set("dockerDeploymentSourcePath", 'build/libs/${project.name}-*-${javaApplicationShadowClassifier}.jar')
	project.ext.set("dockerDeploymentSourcePath", "build/libs/${jarRunnerFile}")
}

apply from: "${commonGradleBuildElementPath}/base/docker.gradle"


/***************************************************************************************
 * excludes
 ***************************************************************************************/
if (project.tasks.findByName('shadowJar')) {
	// exclude a file from Shadow Jar
	shadowJar {
		// allo service files
		mergeServiceFiles()
		
		// in case we have spring
		append("META-INF/spring.handlers")
		append("META-INF/spring.schemas")
		append("META-INF/spring.tooling")
		/*
		transform(PropertiesFileTransformer().apply {
			paths = listOf("META-INF/spring.factories")
			mergeStrategy = "append"
		})
		*/
		
		// excluse
		exclude "META-INF/*.SF"
		exclude "META-INF/*.DSA"
		exclude "META-INF/*.RSA"
		exclude "META-INF/LICENSE"
		exclude "META-INF/VERSION"
		
		if( project.hasProperty('javaApplicationShadowClassifier') ) {
			classifier = project.getProperty('javaApplicationShadowClassifier')
		} else {
			classifier = 'runner'
		}
		
		doLast {
			def filename = prepareFilename(shadowJar.archivePath.getPath())
			printInfo("> Created package " + colorize(HIGHLITE_LEVEL, filename) + ".")
		}
	}
}


/***************************************************************************************
 * parameters can be passed e.g.: "-Dexec.args="--iterations 10 --runs 10 jptools file --sync""
 ***************************************************************************************/
run {
    if (System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}


/***************************************************************************************
 * define the main class name (will be done in build.gradle))
 ***************************************************************************************/
//mainClassName = "my.packname.classname"
